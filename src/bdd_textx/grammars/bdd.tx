import base
import scene

Model:
imports*=Import

namespaces=NamespaceDeclare*

(
behaviours+=Behaviour
|
events+=Event
)*

(
templates+=ScenarioTemplate
|
stories+=UserStory
)+
;

Behaviour: 'Behaviour' name=ID ;

Event: 'Event' name=ID ;

UserStory:
'User' 'Story' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')'  name=ID '{'

'As' 'A' role=STRING
'I' 'Want' feature=STRING
'So' 'That' benefit=STRING

'Scenarios' ':'
    scenarios=ScenarioVariant+
'}'
;

ScenarioVariant:
'Scenario' name=ID '{'
    'template' ':' '<' template=[ScenarioTemplate|FQN] '>'
    'scene' ':' '<' template=[SceneModel|FQN] '>'

    (variations=TaskVariation+)?

    ('Given' ':' given_clauses=Clause+)?
    ('When' ':' when_event_clauses=EventWhenClause+)?
    ('Then' ':' then_clauses=Clause+)?
'}'
;

TaskVariation: 'var' '<' variable=[ScenarioVariable|FQN] '>' 'can' 'be' ':'
(
'-' 'obj' '<' objects+=[Object|FQN] '>'
|
'-' 'ws' '<' workspaces+=[Workspace|FQN] '>'
|
'-' 'agn' '<' agents+=[Agent|FQN] '>'
)*
;

ScenarioTemplate:
'Scenario' 'Template' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')' name=ID '{'
    variables=ScenarioVariable*

    'Given' ':'
        given_clauses=Clause+

    'When' ':'
        when_event_clauses=EventWhenClause*
        when_behaviour=BehaviourWhenClause

    'Then' ':'
        then_clauses=Clause+
'}'
;

ScenarioVariable: 'var' name=ID;

// When Clauses
EventWhenClause: 'event' '<' event=[Event|FQN] '>' 'occurs';

BehaviourWhenClause: AgentBehaviourWhenClause ;
AgentBehaviourWhenClause: '<' agent=[ScenarioVariable|FQN] '>' 'executes'  '<' behaviour=[Behaviour|FQN] '>' ;

// Time constraints
TimeConstraint: BeforeBehaviour | AfterBehaviour | DuringBehaviour ;
BeforeBehaviour: 'before' '<' behaviour=[Behaviour|FQN] '>';
AfterBehaviour: 'after' '<' behaviour=[Behaviour|FQN] '>';
DuringBehaviour: 'during' '<' behaviour=[Behaviour|FQN] '>';

// Given/Then clauses
Clause: LocatedAtFluentClause | DoesNotDropFluentClause | TextClause;
TextClause: text=STRING tc=TimeConstraint;
LocatedAtFluentClause:
    '<' object=[ScenarioVariable|FQN] '>' 'is' 'located' 'at' '<' workspace=[ScenarioVariable|FQN] '>'
    tc=TimeConstraint ;
DoesNotDropFluentClause:
    '<' agent=[ScenarioVariable|FQN] '>' 'does' 'not' 'drop' '<' object=[ScenarioVariable|FQN] '>'
    tc=TimeConstraint ;
