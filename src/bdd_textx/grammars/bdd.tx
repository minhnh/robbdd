import base
import scene

Model:
imports*=Import

namespaces=NamespaceDeclare*

(
events+=Event
|
tasks+=Task
)*

(
templates+=ScenarioTemplate
|
stories+=UserStory
)+
;

Behaviour: 'Behaviour' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')' name=ID ;

Event: 'Event' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')' name=ID ;

Task: 'Task' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')' name=ID ;

UserStory:
'User' 'Story' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')'  name=ID '{'

'As' 'A' role=STRING
'I' 'Want' feature=STRING
'So' 'That' benefit=STRING

'Scenarios' ':'
    scenarios=ScenarioVariant+
'}'
;

ScenarioVariant:
'Scenario' name=ID '{'
    'template' ':' '<' template=[ScenarioTemplate|FQN] '>'
    'scene' ':' '<' scene=[SceneModel|FQN] '>'

    (variation=TaskVariation)?

    ('Given' ':' given_clauses=Clause+)?
    ('When' ':' when_event_clauses=WhenEventClause+)?
    ('Then' ':' then_clauses=Clause+)?
'}'
;

TaskVariation: CartesianProductVariation | TableVariation ;

CartesianProductVariation: var_entries+=VarEntry ;

VarEntry: 'var' '<' variable=[VariableBase|FQN] '>' 'can' 'be' ':'
(
'-' 'obj' '<' objects+=[Object|FQN] '>'
|
'-' 'ws' '<' workspaces+=[Workspace|FQN] '>'
|
'-' 'agn' '<' agents+=[Agent|FQN] '>'
)*
;

TableVariation: rows+=Row ;

Row: '|' cells+=Cell ;

Cell: Value '|' ;

Value:
(
'var' '<' variable=[VariableBase|FQN] '>'
|
'obj' '<' object=[Object|FQN] '>'
|
'ws' '<' workspace=[Workspace|FQN] '>'
|
'agn' '<' agent=[Agent|FQN] '>'
)
;

ScenarioTemplate:
'Scenario' 'Template' '(' 'ns' '=' ns=[NamespaceDeclare|FQN] ')' name=ID '{'
    'task' ':' '<' task=[Task|FQN] '>'

    variables=VariableBase*
    gwt_expr=GivenWhenThenExpr
'}'
;

SetBase: ConstantSet | ScenarioSetVariable ;
ConstantSet:
'const' 'set' name=ID '{'
'}'
;

VariableBase: ScenarioVariable | ScenarioSetVariable ;
ScenarioSetVariable: 'set' 'var' name=ID ;
ScenarioVariable: 'var' name=ID ;

// When Clauses
WhenEventClause: 'event' '<' event=[Event|FQN] '>' 'occurs';

WhenBehaviourClause:
behaviour=Behaviour ':' param_bhv=ParameterizedBehaviour
;
ParameterizedBehaviour: PickPlaceBehaviour | PickBehaviour | PlaceBehaviour ;
PickBehaviour:
    '<' agent=[VariableBase|FQN] '>' 'picks' '<' object=[VariableBase|FQN] '>'
;
PlaceBehaviour:
    '<' agent=[VariableBase|FQN] '>' 'places' '<' object=[VariableBase|FQN] '>' 'at' '<' workspace=[VariableBase|FQN] '>'
;
PickPlaceBehaviour:
    '<' agent=[VariableBase|FQN] '>' 'picks' '<' object=[VariableBase|FQN] '>' 'and'
        'places' 'at' '<' workspace=[VariableBase|FQN] '>'
;

// Time constraints
TimeConstraint: BeforeEvent | AfterEvent | DuringEvent ;
BeforeEvent: 'before' '<' event=[Event|FQN] '>';
AfterEvent: 'after' '<' event=[Event|FQN] '>';
DuringEvent: 'from' '<' start_event=[Event|FQN] '>' 'until' '<' end_event=[Event|FQN] '>';

// Given/Then clauses
Clause: TextClause | FluentLogicExpr | ExistsExpr ;
TextClause: text=STRING tc=TimeConstraint;

// Scenario terms
GivenExpr: 'Given' ':' given=Clause ;
WhenExpr:
    'When' ':'
        when_events=WhenEventClause*
        when_bhv=WhenBehaviourClause
;
ThenExpr: 'Then' ':' then=Clause ;
GivenWhenThenExpr:
    (given_expr=GivenExpr)?
    (
    when_expr=WhenExpr
    |
    forall_expr=ForAllExpr
    )
    (then_expr=ThenExpr)?
;

// First Order Logic terms
FOLExpr: LocatedAtPred | IsHeldPred | DoesNotDropPred | CanReachPred | HasConfigPred | IsSortedPred ;
CanReachPred:
    '<' agent=[VariableBase|FQN] '>' 'can' 'reach' '<' object=[VariableBase|FQN] '>'
;
LocatedAtPred:
    '<' object=[VariableBase|FQN] '>' 'is' 'located' 'at' '<' workspace=[VariableBase|FQN] '>'
;
DoesNotDropPred:
    '<' agent=[VariableBase|FQN] '>' 'does' 'not' 'drop' '<' object=[VariableBase|FQN] '>'
;
IsHeldPred:
    '<' object=[VariableBase|FQN] '>' 'is' 'held' 'by' '<' agent=[VariableBase|FQN] '>'
;
HasConfigPred:
    '<' subject=[VariableBase|FQN] '>' 'has' 'config' '<' config=[VariableBase|FQN] '>'
;
IsSortedPred:
    '<' objects=[ScenarioSetVariable|FQN] '>' 'are' 'sorted' 'into' '<' workspaces=[ScenarioSetVariable|FQN] '>'
;

// Fluent logic terms composing holds clauses with basic logic operators
FluentLogicExpr: FluentAndExpr | FluentOrExpr | FluentNotExpr | HoldsExpr ;
HoldsExpr:
'holds' '(' predicate=FOLExpr ',' tc=TimeConstraint ')'
;
FluentAndExpr:
'('
  first=FluentLogicExpr
  ('and' rest=FluentLogicExpr)*
')'
;
FluentOrExpr:
'('
  first=FluentLogicExpr
  ('or' rest=FluentLogicExpr)*
')'
;
FluentNotExpr:
'(' 'not' expr=FluentLogicExpr ')'
;


// quantifiers over fluent logic expressions
ForAllExpr:
'for' 'all' '(' var=ScenarioVariable 'in' '<' in_set=[SetBase|FQN] '>' ')' '{'
    gwt_expr=GivenWhenThenExpr
'}'
;
ExistsExpr:
'(' var=ScenarioVariable 'exists' 'in' '<' in_set=[SetBase|FQN] '>' ')' 'such' 'that' '{'
    fl_expr=FluentLogicExpr
'}'
;
